eval "$(/opt/homebrew/bin/brew shellenv)"
export EDITOR=vim
export VISUAL="$EDITOR"
export GOPATH="$HOME/go"
export PATH="$PATH:$HOME/go/bin:$HOME/bin"
export PATH="$HOME/bin:$PATH"
export ASDF_DATA_DIR="$HOME/.asdf"
export PATH="$ASDF_DATA_DIR/shims:$PATH"
export PATH="$PATH:$HOME/npm-global/bin"
# Needed because this must be set for tilt if I don't have a tilt-config.json
# which I don't because I use git worktrees
export TILT_USE_DEV_VAULT_SPREADING=true

# The next line updates PATH for the Google Cloud SDK.
if [ -f '/Users/xavier/google-cloud-sdk/path.zsh.inc' ]; then . '/Users/xavier/google-cloud-sdk/path.zsh.inc'; fi

# The next line enables shell command completion for gcloud.
if [ -f '/Users/xavier/google-cloud-sdk/completion.zsh.inc' ]; then . '/Users/xavier/google-cloud-sdk/completion.zsh.inc'; fi

# The next line prepares a gcurl command to query metrics.
alias gcurl='curl -H "Authorization: Bearer $(gcloud auth print-access-token)" -H "Content-Type: application/json"'

# The next line enables shell command completion for kubectl.
source <(kubectl completion zsh)

# The next line enables shell command completion for tenv
source <(tenv completion zsh)
export TENV_QUIET=true # Comment out to see more info
export TENV_AUTO_INSTALL=true
#export TENV_LOG=debug # Remove comment if need more info
#export TENV_DETACHED_PROXY=false

# emacs M-x shell
if [[ $TERM == "dumb" ]]; then
  alias less='cat'
  alias more='cat'
  export PAGER=cat
  export TERM=xterm-256color
  # prevent tramp from hanging on remote ssh zsh connection
  # disable zsh line editor
  unsetopt zle
  # fix prompt behavior
  unsetopt prompt_cr prompt_sp
  # simplify the prompt
  # PS1='$ '
fi

# emacs M-x vterm
if [[ "$INSIDE_EMACS" = 'vterm' ]] \
    && [[ -n ${EMACS_VTERM_PATH} ]] \
    && [[ -f ${EMACS_VTERM_PATH}/etc/emacs-vterm-bash.sh ]]; then
	source ${EMACS_VTERM_PATH}/etc/emacs-vterm-bash.sh
fi

# better defaults for zsh history
export HISTSIZE=1000000   # the number of items for the internal history list
export SAVEHIST=1000000   # maximum number of items for the history file

# The meaning of these options can be found in man page of `zshoptions`.
setopt HIST_IGNORE_ALL_DUPS  # do not put duplicated command into history list
setopt HIST_SAVE_NO_DUPS  # do not save duplicated command
setopt HIST_REDUCE_BLANKS  # remove unnecessary blanks
setopt INC_APPEND_HISTORY_TIME  # append command to history file immediately after execution
# setopt EXTENDED_HISTORY  # record command start time

# configure prompt with git info
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git svn
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '^'
zstyle ':vcs_info:*' unstagedstr '*'
zstyle ':vcs_info:git:*' formats '%b|%u|%c'
zstyle ':vcs_info:git:*' actionformats '%b|%u|%c|%a'

git_prompt_path() {
  local gitroot=$(git rev-parse --show-toplevel 2>/dev/null)
  if [[ -n $gitroot ]]; then
    local gitname=$(basename $gitroot)
    local relpath=${PWD#$gitroot/}
    [[ $relpath == $PWD ]] && echo "$gitname" || echo "$gitname/$relpath"
  else
    echo "%~"
  fi
}

git_prompt_info() {
  if [[ -z $vcs_info_msg_0_ ]]; then
    return
  fi

  local parts=(${(s:|:)vcs_info_msg_0_})
  local branch=$parts[1]
  local unstaged=$parts[2]
  local staged=$parts[3]
  local action=$parts[4]

  local gitroot=$(git rev-parse --show-toplevel 2>/dev/null)
  local gitname=$(basename $gitroot)

  local branch_display
  if [[ $branch == $gitname ]]; then
    branch_display="[-]"
  else
    branch_display="[$branch]"
  fi

  local git_status="${staged}${unstaged}"
  local action_display=""
  [[ -n $action ]] && action_display=" ($action)"

  echo "${branch_display}${git_status}${action_display}"
}

precmd() { vcs_info }

setopt PROMPT_SUBST
PROMPT='%(?.%K{10}%F{black}.%K{9}%F{black})$(git_prompt_path) $(git_prompt_info) >%k%f '

# allow comments in the interactive command line
setopt INTERACTIVE_COMMENTS

# create zsh line editor keymap for alias expansion
aliasmap() {
  zle _expand_alias
  zle expand-word
  zle self-insert
}

zle -N aliasmap

bindkey " " aliasmap
bindkey "^ " magic-space           # control-space to bypass completion
bindkey -M isearch " " magic-space # normal space during searches

# C-x C-e to edit command line in editor
autoload -z edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

#THIS MUST BE AT THE END OF THE FILE FOR SDKMAN TO WORK!!!
export SDKMAN_DIR="$HOME/.sdkman"
[[ -s "$HOME/.sdkman/bin/sdkman-init.sh" ]] && source "$HOME/.sdkman/bin/sdkman-init.sh"
